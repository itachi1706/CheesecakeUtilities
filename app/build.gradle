apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'io.fabric'

repositories {
    maven { url "https://jitpack.io" }
    mavenCentral()
}


def isTravis = System.getenv("TRAVIS")

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        applicationId "com.itachi1706.cheesecakeutilities"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 609
        versionName "4.2.2"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        resValue "string", "tray__authority", "${applicationId}.tray"
        // add this to set a unique tray authority based on your applicationId
        resConfigs "en"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }
    Properties props = new Properties()
    def propFile = file('key.properties')
    //check if SAFETY_NET_DEVICE_VERIFICATION_API_KEY is defined in gradle.properties (this is so the key is not in github)
    String GOOGLE_VERIFICATION_API_KEY = ""
    if (propFile.canRead()) {
        props.load(new FileInputStream(propFile))
        if (props != null) {
            if (props.containsKey('key.safetynet')) {
                GOOGLE_VERIFICATION_API_KEY = props.getProperty('key.safetynet')
            }
        }
    }
    buildTypes {
        release {
            buildConfigField("String", "GOOGLE_VERIFICATION_API_KEY", "\"" + GOOGLE_VERIFICATION_API_KEY + "\"")
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            buildConfigField("String", "GOOGLE_VERIFICATION_API_KEY", "\"" + GOOGLE_VERIFICATION_API_KEY + "\"")
            // Disable Fabric Stuff (Deprecated, it is now disabled by default and enabled in runtime)
            //ext.enableCrashlytics = false
            if (isTravis) {
                minifyEnabled true
            }
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    // This enables long timeouts required on slow environments, e.g. Travis
    adbOptions {
        timeOutInMs 10 * 60 * 1000  // Set the timeout to 10 minutes
        installOptions "-d", "-t"
    }
    dexOptions {
        preDexLibraries = !isTravis
    }
}

ext {
    supportLibraryVersion = '27.1.1'
    playServicesVersion = '15.0.0'
}

dependencies {
    // Global Dependencies
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "com.android.support:support-v4:$supportLibraryVersion"
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation 'com.android.support.test.espresso:espresso-contrib:3.0.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.1'
    implementation project(':appupdater')
    implementation "com.android.support:design:$supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$supportLibraryVersion"
    implementation "com.google.firebase:firebase-core:$playServicesVersion"
    implementation "com.google.firebase:firebase-config:$playServicesVersion"
    implementation "com.google.firebase:firebase-perf:$playServicesVersion"
    implementation 'commons-io:commons-io:2.5'
    implementation 'org.apache.commons:commons-lang3:3.5'
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'de.psdev.licensesdialog:licensesdialog:1.8.3'
    // Specific Utility Dependencies
    // Navbar Utility
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'net.grandcentrix.tray:tray:0.11.0'
    implementation 'com.github.QuadFlask:colorpicker:0.0.12'
    // ORD Countdown Utility
    implementation 'com.github.lzyzsd:circleprogress:1.2.0@aar'
    // Application List Utility
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.2'
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'
    implementation 'com.github.turing-tech:MaterialScrollBar:13.0.1'
    // SafetyNet Validator
    implementation 'com.github.scottyab:safetynethelper:0.3.0'
    implementation "com.google.android.gms:play-services-safetynet:$playServicesVersion"
    // Barcode Tools
    implementation "com.google.android.gms:play-services-vision:$playServicesVersion"
    implementation 'com.google.zxing:core:3.3.1'
    // Lyric Finder
    implementation "com.android.support:palette-v7:$supportLibraryVersion"
    // Vehicle Mileage Tracker
    implementation "com.google.android.gms:play-services-auth:$playServicesVersion"
    implementation "com.google.firebase:firebase-database:$playServicesVersion"
    implementation "com.google.firebase:firebase-auth:$playServicesVersion"
    implementation 'com.getbase:floatingactionbutton:1.10.1'
    // Others
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'
    testImplementation 'junit:junit:4.12'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

apply plugin: 'com.google.gms.google-services'