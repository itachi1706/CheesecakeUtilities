buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.21.6'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
}


def isTravis = System.getenv("TRAVIS")

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    defaultConfig {
        applicationId "com.itachi1706.cheesecakeutilities"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 369
        versionName "3.1.0"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        resValue "string", "tray__authority", "${applicationId}.tray"
        // add this to set a unique tray authority based on your applicationId
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }
    def Properties props = new Properties()
    def propFile = file('key.properties')
    //check if SAFETY_NET_DEVICE_VERIFICATION_API_KEY is defined in gradle.properties (this is so the key is not in github)
    String GOOGLE_VERIFICATION_API_KEY = "";
    if (propFile.canRead()) {
        props.load(new FileInputStream(propFile))
        if (props != null && props.containsKey('key.safetynet')) {
            GOOGLE_VERIFICATION_API_KEY = props.getProperty('key.safetynet')
        }
    }
    buildTypes {
        release {
            buildConfigField("String", "GOOGLE_VERIFICATION_API_KEY", "\"" + GOOGLE_VERIFICATION_API_KEY + "\"")
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            buildConfigField("String", "GOOGLE_VERIFICATION_API_KEY", "\"" + GOOGLE_VERIFICATION_API_KEY + "\"")
            // Disable Fabric Stuff
            ext.enableCrashlytics = false
        }
    }
    // This enables long timeouts required on slow environments, e.g. Travis
    adbOptions {
        timeOutInMs 10 * 60 * 1000  // Set the timeout to 10 minutes
        installOptions "-d", "-t"
    }
    dexOptions {
        preDexLibraries = !isTravis
    }
}

dependencies {
    // Global Dependencies
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestCompile 'com.android.support.test.espresso:espresso-contrib:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }
    compile('com.crashlytics.sdk.android:crashlytics:2.6.6@aar') {
        transitive = true;
    }
    compile project(':appupdater')
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'
    compile 'com.google.firebase:firebase-crash:10.2.6'
    compile 'com.google.firebase:firebase-core:10.2.6'
    compile 'com.google.firebase:firebase-config:10.2.6'
    compile 'com.google.firebase:firebase-perf:10.2.6'
    compile 'commons-io:commons-io:2.5'
    compile 'org.apache.commons:commons-lang3:3.5'
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'com.afollestad:digitus:0.3.0' // Fingerprint Authentication
    // Specific Utility Dependencies
    compile 'com.squareup.picasso:picasso:2.5.2' // Navbar Utility
    compile 'net.grandcentrix.tray:tray:0.11.0' // Navbar Utility
    compile 'com.github.QuadFlask:colorpicker:0.0.12' // Navbar Utility
    compile 'com.github.lzyzsd:circleprogress:1.1.0@aar' // ORD Countdown Utility
    compile 'com.github.PhilJay:MPAndroidChart:v3.0.1' // Application List Utility
    compile 'com.afollestad.material-dialogs:core:0.9.4.4' // Application List Utility
    compile 'com.github.turing-tech:MaterialScrollBar:12.3.5' // Application List Utility
    compile 'com.github.scottyab:safetynethelper:0.3.0' // SafetyNet Validator
    compile 'com.google.android.gms:play-services-safetynet:10.2.6' // SafetyNet Validator
    testCompile 'junit:junit:4.12'
}

apply plugin: 'com.google.gms.google-services'