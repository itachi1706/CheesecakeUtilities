buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.24.4'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
}


def isTravis = System.getenv("TRAVIS")

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"

    defaultConfig {
        applicationId "com.itachi1706.cheesecakeutilities"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 474
        versionName "3.4.4"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        resValue "string", "tray__authority", "${applicationId}.tray"
        // add this to set a unique tray authority based on your applicationId
        resConfigs "en"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }
    Properties props = new Properties()
    def propFile = file('key.properties')
    //check if SAFETY_NET_DEVICE_VERIFICATION_API_KEY is defined in gradle.properties (this is so the key is not in github)
    String GOOGLE_VERIFICATION_API_KEY = ""
    String GOOGLE_ID = ""
    if (propFile.canRead()) {
        props.load(new FileInputStream(propFile))
        if (props != null) {
            if (props.containsKey('key.safetynet')) {
                GOOGLE_VERIFICATION_API_KEY = props.getProperty('key.safetynet')
            }
            if (props.containsKey('key.googleid')) {
                GOOGLE_ID = props.getProperty('key.googleid')
            }
        }
    }
    buildTypes {
        release {
            buildConfigField("String", "GOOGLE_VERIFICATION_API_KEY", "\"" + GOOGLE_VERIFICATION_API_KEY + "\"")
            buildConfigField("String", "GOOGLE_ID", "\"" + GOOGLE_ID + "\"")
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            buildConfigField("String", "GOOGLE_VERIFICATION_API_KEY", "\"" + GOOGLE_VERIFICATION_API_KEY + "\"")
            buildConfigField("String", "GOOGLE_ID", "\"" + GOOGLE_ID + "\"")
            // Disable Fabric Stuff
            ext.enableCrashlytics = false
        }
    }
    // This enables long timeouts required on slow environments, e.g. Travis
    adbOptions {
        timeOutInMs 10 * 60 * 1000  // Set the timeout to 10 minutes
        installOptions "-d", "-t"
    }
    dexOptions {
        preDexLibraries = !isTravis
    }
}

dependencies {
    // Global Dependencies
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation 'com.android.support.test.espresso:espresso-contrib:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }
    implementation('com.crashlytics.sdk.android:crashlytics:2.7.1@aar') {
        transitive = true
    }
    implementation project(':appupdater')
    implementation 'com.android.support:design:26.0.1'
    implementation 'com.android.support:cardview-v7:26.0.1'
    implementation 'com.google.firebase:firebase-crash:11.6.0'
    implementation 'com.google.firebase:firebase-core:11.6.0'
    implementation 'com.google.firebase:firebase-config:11.6.0'
    implementation 'com.google.firebase:firebase-perf:11.6.0'
    implementation 'commons-io:commons-io:2.5'
    implementation 'org.apache.commons:commons-lang3:3.5'
    implementation 'com.google.code.gson:gson:2.8.0'
    implementation 'de.psdev.licensesdialog:licensesdialog:1.8.3'
    implementation 'com.afollestad:digitus:0.3.0' // Fingerprint Authentication
    // Specific Utility Dependencies
    implementation 'com.squareup.picasso:picasso:2.5.2' // Navbar Utility
    implementation 'net.grandcentrix.tray:tray:0.11.0' // Navbar Utility
    implementation 'com.github.QuadFlask:colorpicker:0.0.12' // Navbar Utility
    implementation 'com.github.lzyzsd:circleprogress:1.1.0@aar' // ORD Countdown Utility
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.1' // Application List Utility
    implementation 'com.afollestad.material-dialogs:core:0.9.4.7' // Application List Utility
    implementation 'com.github.turing-tech:MaterialScrollBar:13.0.1' // Application List Utility
    implementation 'com.github.scottyab:safetynethelper:0.3.0' // SafetyNet Validator
    implementation 'com.google.android.gms:play-services-safetynet:11.6.0' // SafetyNet Validator
    implementation 'com.google.android.gms:play-services-auth:11.6.0' // Vehicle Mileage Tracker
    implementation 'com.google.firebase:firebase-database:11.6.0' // Vehicle Mileage Tracker
    implementation 'com.google.firebase:firebase-auth:11.6.0' // Vehicle Mileage Tracker
    implementation 'com.getbase:floatingactionbutton:1.10.1' // Vehicle Mileage Tracker
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    testImplementation 'junit:junit:4.12'
}

apply plugin: 'com.google.gms.google-services'