apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'io.fabric'

repositories {
    maven { url "https://jitpack.io" }
    mavenCentral()
}


def isTravis = System.getenv("TRAVIS")

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        applicationId "com.itachi1706.cheesecakeutilities"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 857
        versionName "4.6.5-beta"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resValue "string", "tray__authority", "${applicationId}.tray"
        // add this to set a unique tray authority based on your applicationId
        resConfigs "en"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }
    Properties props = new Properties()
    def propFile = file('key.properties')
    //check if SAFETY_NET_DEVICE_VERIFICATION_API_KEY is defined in gradle.properties (this is so the key is not in github)
    String GOOGLE_VERIFICATION_API_KEY = ""
    if (propFile.canRead()) {
        props.load(new FileInputStream(propFile))
        if (props != null) {
            if (props.containsKey('key.safetynet')) {
                GOOGLE_VERIFICATION_API_KEY = props.getProperty('key.safetynet')
            }
        }
    }
    buildTypes {
        release {
            buildConfigField("String", "GOOGLE_VERIFICATION_API_KEY", "\"" + GOOGLE_VERIFICATION_API_KEY + "\"")
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            proguardFile 'proguard-simple-xml.pro'
            proguardFile 'proguard-google-api-client.pro'
        }
        debug {
            buildConfigField("String", "GOOGLE_VERIFICATION_API_KEY", "\"" + GOOGLE_VERIFICATION_API_KEY + "\"")
            ext.alwaysUpdateBuildId = false
            // Disable Fabric Stuff (Deprecated, it is now disabled by default and enabled in runtime)
            //ext.enableCrashlytics = false
            //if (isTravis) {
            multiDexEnabled true
            //}
        }
    }
    // This enables long timeouts required on slow environments, e.g. Travis
    adbOptions {
        timeOutInMs 10 * 60 * 1000  // Set the timeout to 10 minutes
        installOptions "-d", "-t"
    }
    dexOptions {
        preDexLibraries = !isTravis
    }
}

ext {
    supportLibraryVersion = '28.0.0+'
}

dependencies {
    // Global Dependencies
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha03'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0-alpha4', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.1.0-alpha4', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.9'
    implementation project(':appupdater')
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.firebase:firebase-core:16.0.8'
    implementation 'com.google.firebase:firebase-config:16.4.1'
    implementation 'com.google.firebase:firebase-perf:16.2.4'
    implementation 'commons-io:commons-io:2.5'
    implementation 'org.apache.commons:commons-lang3:3.6'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'de.psdev.licensesdialog:licensesdialog:1.8.3'
    // Specific Utility Dependencies
    // Navbar Utility
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'net.grandcentrix.tray:tray:0.11.0'
    implementation 'com.github.QuadFlask:colorpicker:0.0.12'
    // ORD Countdown Utility
    implementation 'com.github.lzyzsd:circleprogress:1.2.0@aar'
    // Application List Utility
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.2'
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0' // TODO: 2.0.0 breaks a lot of stuff, find time to migrate shit over
    implementation 'com.github.turing-tech:MaterialScrollBar:13.0.1'
    // SafetyNet Validator
    implementation 'com.google.android.gms:play-services-safetynet:16.0.0'
    // Barcode Tools
    implementation 'com.google.firebase:firebase-ml-vision:19.0.3'
    implementation 'com.google.zxing:core:3.3.1'
    // Lyric Finder
    implementation 'androidx.palette:palette:1.0.0'
    // Vehicle Mileage Tracker
    implementation 'com.google.android.gms:play-services-auth:16.0.1'
    implementation 'com.google.firebase:firebase-database:16.1.0'
    implementation 'com.google.firebase:firebase-auth:16.2.0'
    implementation 'com.getbase:floatingactionbutton:1.10.1'
    // CEPAS Reader
    implementation project(':cepaslib')
    // MSL Sync Integration
    implementation 'com.google.api-client:google-api-client-android:1.23.0', {
        exclude group: "org.apache.httpcomponents"
        exclude module: 'google-http-client'
    }
    implementation 'com.google.apis:google-api-services-calendar:v3-rev364-1.25.0', {
        exclude group: "org.apache.httpcomponents"
        exclude module: 'google-http-client'
    }
    implementation 'com.google.http-client:google-http-client-gson:1.23.0', {
        exclude group: "org.apache.httpcomponents"
    }
    implementation 'com.firebase:firebase-jobdispatcher:0.8.5'
    // Others
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:rules:1.1.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.biometric:biometric:1.0.0-alpha03'
    implementation 'com.google.guava:guava:27.0.1-android'
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'kotlin-android-extensions'